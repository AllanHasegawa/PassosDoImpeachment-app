apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    def config = rootProject.ext.androidConfig

    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    defaultConfig {
        applicationId config.applicationId
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner config.testInstrumentationRunner

        multiDexEnabled = config.multiDexEnabled

        testProguardFiles "test-proguard-rules.pro"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("DIAPP_STORE_FILE"))
            storePassword System.getenv("DIAPP_STORE_PASSWORD")
            keyAlias System.getenv("DIAPP_KEY_ALIAS")
            keyPassword System.getenv("DIAPP_KEY_PASSWORD")
        }
    }

    testBuildType "release"

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += 'src/main/java'
        debug.java.srcDirs += 'src/debug/java'
    }
    packagingOptions {
        pickFirst 'META-INF/services/javax.annotation.processing.Processor'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

kapt {
    generateStubs = true
}


configurations.all {
    resolutionStrategy {
        force "com.android.support:recyclerview-v7:23.2.1", "com.android.support:design:23.2.1",
                "com.android.support:support-annotations:23.2.1"
    }
}

dependencies {
    compile project(":domain")
    compile project(":db")
    compile project(":cloud")
    compile project(":presentation")

    def androidTestDependencies = rootProject.ext.appDeps.androidTest
    def debugDependencies = rootProject.ext.appDeps.debug
    def dependencies = rootProject.ext.appDeps.deps
    def kaptDependencies = rootProject.ext.appDeps.kapt

    androidTestDependencies.each { androidTestCompile it }
    debugDependencies.each { debugCompile it }
    dependencies.each { compile it }
    kaptDependencies.each { kapt it }
}

apply plugin: 'com.google.gms.google-services'
